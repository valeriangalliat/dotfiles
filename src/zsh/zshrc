#
# ~/.zshrc
#

# Initialization {{{
# ==================

    export ZSH=$DOTFILES/deps/zsh/oh-my-zsh

    # Theme
    ZSH_THEME=robbyrussell

    # Do not auto update `oh-my-zsh` (performance)
    DISABLE_AUTO_UPDATE=true

    # Correct mistyped commands
    # ENABLE_CORRECTION=true

    # No title in tmux
    if [ -n "$TMUX" ]; then
        DISABLE_AUTO_TITLE=true
    fi

    # Don't mark untracked files as dirty (performance)
    DISABLE_UNTRACKED_FILES_DIRTY=true

    # Custom `oh-my-zsh` directory
    ZSH_CUSTOM=$DOTFILES/src/zsh

    # Enabled plugins
    plugins=(
        git
        git-extras
        colored-man
        zsh-syntax-highlighting
        history-substring-search
        z
    )

    # Load core
    source "$ZSH/oh-my-zsh.sh"

    # Remove `oh-my-zsh` function which sets current command as title
    preexec_functions=${preexec_functions#omz_termsupport_preexec}

# }}}

# Essentials {{{
# ==============

    setopt interactivecomments

# }}}

# Theme {{{

    # Terminal colors (except in tmux)
    if [ -z "$TMUX" ]; then
        source $DOTFILES/deps/shell/base16-shell/base16-default.dark.sh
    fi

    # Apply `LS_COLORS` to completion
    zstyle ':completion:*:default' list-colors "${(s.:.)LS_COLORS}"

# }}}

# Aliases {{{
# ===========

    # Standard {{{
    # ------------

        # `GREP_OPTIONS` is deprecated
        alias grep="grep $GREP_OPTIONS"
        unset GREP_OPTIONS

        if [ "$(uname -s)" = Linux ]; then
            # Directories first (only on Linux), extend existing alias
            alias ls="$(whence ls) --group-directories-first"
        fi

        # Shortcuts
        alias l=ll
        alias t='tail -f'
        alias q=exit
        alias j=jobs
        alias r=reset
        alias c=clear
        alias h=history

        # Git plugin additions
        alias gcam='gc -am'
        alias gcam!='gc --amend -am'
        alias gdw='gd -w'
        alias gf='g fetch'
        alias grb='g rebase'

        # Same as Git plugin but without `--max-count`
        alias glg='g log --stat'
        alias glgg='g log --graph'

        # Websites
        alias cj=codejam.info
        alias vcj=val.codejam.info
        alias mcj=mail.codejam.info
        alias tw=twitter.com
        alias gh=github.com
        alias jvc=jeuxvideo.com
        alias gmail=mail.google.com

        # Configuration files
        alias profile="$EDITOR $DOTFILES/src/shell/profile"
        alias zshrc="$EDITOR $DOTFILES/src/zsh/zshrc"
        alias vimrc="$EDITOR $DOTFILES/src/vim/vimrc"
        alias xresources="$EDITOR $DOTFILES/src/x11/resources"
        alias xsession="$EDITOR $DOTFILES/src/x11/session"

        # Globbing move command
        autoload zmv
        alias mmv='noglob zmv -W'

        # Get remote IP from SFR box homepage (absolute grep to skip alias)
        alias ipb='curl -s 192.168.1.1 | awk "/Adresse IP/{getline;print;exit}" | /usr/bin/grep -Eo "[0-9\.]+"'

        # Get remote IP from CodeJam (at home)
        alias ipc='curl ip.codejam.info && echo'

        # Get remote IP from Deblan (can't use home server when in LAN)
        alias ipd='curl ip.deblan.org && echo'

    # }}}

    # Global {{{
    # ----------

        alias -g H='| head'
        alias -g T='| tail'
        alias -g S='| sort'
        alias -g G='| grep'
        alias -g L='| less'
        alias -g C='| cut'
        alias -g A='| awk'
        alias -g W='| wc'
        alias -g WC='| wc -c'
        alias -g WM='| wc -m'
        alias -g WL='| wc -l'
        alias -g WW='| wc -w'

    # }}}

    # Suffix {{{
    # ----------

        for x in htm html com org net info; do
            alias -s $x=$BROWSER
        done

        #
        # https://en.wikipedia.org/wiki/List_of_file_formats#Graphics
        #
        for x in bmp gif ico jpg jpeg png svg; do
            alias -s $x=$IMAGE_VIEWER
        done

        #
        # https://en.wikipedia.org/wiki/List_of_file_formats#Sound_and_music
        # https://en.wikipedia.org/wiki/List_of_file_formats#Video
        #
        for x in flac m4a mp3 ogg oga wav avi flv mkv mpv mpeg mpg mpe mp4 webm; do
            alias -s $x=$MEDIA_PLAYER
        done

        unset x

        alias -s pdf=zathura

        # List archives
        alias -s zip='unzip -l'
        alias -s rar='unrar l'
        alias -s tar='tar tf'
        alias -s tar.gz='tar tzf'
        alias -s tar.bz2='tar tjf'

    # }}}

# }}}

# Functions {{{
# =============

    # Play a video from a jeuxvideo.com page
    jvcp() {
        "$MEDIA_PLAYER" $("$DOTFILES/deps/misc/jvc-tools/video" "$1")
    }

# }}}
